\section{Implementation of \dsl{}}
\label{sec:detail}

This section goes over the definition of the operations used earlier and redefines the animations to show the resulting type signature. We develop \dsl{} in the mtl-style, which derives its name from the Haskell \texttt{mtl} library\footnote{\url{http://hackage.haskell.org/package/mtl}} implementing monadic effects using typeclasses \cite{DBLP:conf/afp/Jones95}. This style is also called the finally tagless approach \cite{DBLP:journals/jfp/CaretteKS09}. However, \dsl{} does not force the monadic constraint on its class definitions and thus provides opportunities for inspectability.

\subsection{Specifying Basic Animations}

Mtl-style effects are based on typeclasses defining the available operations for a particular effect. For example, we specify the \hs{basic} operation using the \hs{Basic} typeclass defined below.

\begin{code}
class Basic obj f where
  basic :: Traversal' obj Float -> Duration -> Target -> f ()
\end{code}

Note that in traditional mtl-style, we would write the class definition as \hs{class Monad f => Basic obj f}. However, this constraint is not necessarily applicable for our DSL since we want to provide inspectable animations. The inspection capabilities of monads are limited, so we leave the class of effect open to leave the possibility for a different constraint.

Using the \hs{basic} function created by the \hs{Basic} typeclass enables us to specify basic animations. Now we can accompany the \hs{line1Outro}, \hs{line2Intro}, \hs{menuSlideIn}, and \hs{appFadeOut} animations from Section~\ref{sec:motivation} with their type signatures. The signature of each animation is \hs{f}~\hs{()}, but with a \hs{Basic Application f} constraint.

\begin{code}
line1Outro :: (Basic Application f) => f ()
line1Outro = basic (navbar . underline1 . width) (For 0.25) (To 0)

line2Intro :: (Basic Application f) => f ()
line2Intro = basic (navbar . underline2 . width) (For 0.25) (To 28)

menuSlideIn :: (Basic Application f) => f ()
menuSlideIn = basic (menu . width) (For 0.5) (To 75)

appFadeOut :: (Basic Application f) => f ()
appFadeOut = basic (obscuringBox . alpha) (For 0.5) (To 0.5)
\end{code}

\subsection{Specifying Composed Animations}

We used the combinators \hs{sequential} and \hs{parallel} for creating composed animations. Based on their use in Section~\ref{sec:motivation}, these have the form \hs{f}~\hs{()}~\hs{->}~\hs{f ()}~\hs{->}~\hs{f}~\hs{()}. However, these functions are limited to animations with no interesting return value. Instead, we generalize these functions to \hs{(}\hs{a}~\hs{->}~\hs{b}~\hs{->}~\hs{c}\hs{)}~\hs{->}~\hs{f}~\hs{a}~\hs{->}~\hs{f}~\hs{b}~\hs{->}~\hs{f}~\hs{c}, which takes two animations returning values of respectively type \hs{a} and \hs{b} and a function which combines these return values. The result is then an animation returning a value of type \hs{c}.

\subsubsection{Sequential Composition}

Instead of defining our own class for sequential animations, we reuse the \hs{Functor}-\hs{Applicative}-\hs{Monad} hierarchy intended for sequencing effects. As a result, the \hs{sequential} function becomes a specialization of the \hs{liftA2} function\footnote{\url{https://hackage.haskell.org/package/base-4.12.0.0/docs/Control-Applicative.html#v:liftA2}}.

\begin{code}
sequential :: (Applicative f) => f () -> f () -> f ()
sequential f1 f2 = liftA2 (\_ _ -> ()) f1 f2
\end{code}

The type signature for \hs{selectBtn2Anim} contains an additional \hs{Applicative f} constraint in addition to the \hs{Basic Application f} constraint.

\begin{code}
selectBtn2Anim :: (Basic Application f, Applicative f) => f ()
selectBtn2Anim = line1Outro `sequential` line2Intro
\end{code}

\subsubsection{Parallel Composition}

The \hs{parallel} function does not correspond to any function in an existing Haskell typeclass. At first we might suspect to find something in the \hs{Alternative} class, but there is no sensible implementation for \hs{mzero} and the potential laws\footnote{\url{https://en.wikibooks.org/wiki/Haskell/Alternative_and_MonadPlus}} do not make sense for animations.

Instead, we create our own \hs{Parallel} typeclass defined below. The function \hs{liftP2} has the same signature as \hs{liftA2}, but its semantics are parallel composition instead of sequential composition. We define the specialized \hs{parallel} function for animations with no return value, and then we can give a type signature to the \hs{menuIntro} definition.

\begin{code}
class Parallel f where
  liftP2 :: (a -> b -> c) -> f a -> f b -> f c

parallel :: (Parallel f) => f () -> f () -> f ()
parallel f1 f2 = liftP2 (\_ _ -> ()) f1 f2

menuIntro :: (Basic Application f, Parallel f) => f ()
menuIntro = menuSlideIn `parallel` appFadeOut
\end{code}

\subsection{Running Animations}

To interpret our DSL into an actual animation we need to provide a data type which implements the typeclasses from the previous sections. We give an explanation of our implementation in the following paragraph.

An animation takes in the current state of our application and the amount of time elapsed since the previous frame, as a result we obtain the next state of the application and the remaining part of the animation --- or the result of the animation if it is finished. Note that the result is wrapped in a type constructor \hs{m} since we can embed custom effects. This is modeled with the type \hs{obj}~\hs{->}~\hs{Float}~\hs{->}~\hs{(obj,}~\hs{m}~\hs{(Either}~\hs{(Animation}~\hs{obj}~\hs{a)}~\hs{a))}. This seems fine, except that we also need to return the remaining duration of the time that is unused. We need this when, for example, we are at the end of an animation and the delta time between frames is longer than that animation. Then, this remaining duration can be used to see how much of the next animation we need to run. Thus, we get the following definition for which we need to provide an instance for each of the type classes \hs{Basic}, \hs{Monad} and \hs{Par}.

\begin{code}
newtype Animation obj m a = Animation {
  runAnimation ::
    obj -> -- previous state
    Float -> -- time delta
    m -- result is wrapped in m
      ( obj -- next state
      , Either
          (Animation obj m a) -- animation remainder
          a -- animation result
      , Maybe Float -- remaining time delta
      )
}
\end{code}

\subsubsection{Basic Instance}

In the implementation for the \hs{basic} function we do three main things: construct the new object state, calculate the remainder of the animation, and calculate the remaining duration of the frame time. The new object state is created by applying the helper function \hs{updateValue} on the old state via the traversal. The remaining animation and duration are both dependent on a condition. If the current duration minus the frame time is bigger than zero, then there is a remainder for this basic animation and all time delta is used up. If it is not bigger than zero, then this basic animation is finished playing and there is still remaining time delta.

\begin{code}
instance (Applicative m) => Basic obj (Animation obj m) where
  basic traversal (For duration) (To target) =
    Animation $ \obj t -> let
    -- construct new object state
    newObj = over traversal (updateValue t duration target) obj
    -- calculate remaining duration of this basic animation
    newDuration = duration - t
    -- create remainder animation / time delta
    (remainingAnim, remainingDelta) =
      if newDuration > 0
      then ( Left (basic traversal (For newDuration) (To target))
           , Nothing
           )
      else (Right (), Just (-newDuration))
    in pure (newObj, remainingAnim, remainingDelta)
\end{code}

The \hs{updateValue} helper function updates the value within the traversal depending on the time delta, duration and target value. The result value is clamped to make sure we do not overshoot the target when the time delta is larger than the remaining duration of the animation.

\begin{code}
updateValue ::
  Float -> -- time delta
  Float -> -- duration
  Float -> -- target value
  Float -> -- current value
  Float -- new value
updateValue t duration target current = let
  speed = (target - current) * t / duration
  newValue = current + speed
  in if target > current
    then min target newValue
    else max target newValue
\end{code}

\subsubsection{Monad Instance}

In the \hs{Monad} instance, we define how animations run in sequence. In the \hs{return} case, we embed an animation into the \hs{Animation} data type. In the \hs{>>=} case, we run the animation function inside the first argument. From this, we obtain a new state, a remaining animation, and remaining time delta. What happens next is dependent on the content of the remainders. If no remaining animation is left, we continue running the animation in the continuation \hs{k} by applying the result \hs{a}. If there is a remaining animation, we repackage this remainder with the continuation. Additionally, we merely \hs{return} the next animation if there is no time delta left. If there was time delta left, then the animation keeps running in this timestep.

\begin{code}
instance (Monad m) => Monad (Animation obj m) where
  return a = Animation $ \obj t -> return (obj, Right a, Just t)
  (Animation f) >>= k = Animation $ \obj t -> do
    -- run first animation and obtain result
    (newObj, animResult, mRemainingDelta) <- f obj t
    case (animResult, mRemainingDelta) of
      (Left anim, Nothing) -> -- return repackaged animation
        return (newObj, Left (anim >>= k), Nothing)
      (Right a, Nothing) -> -- return animation in k
        return (newObj, Left (k a), Nothing)
      (Left anim, Just remainingDelta) -> -- run repackaged animation
        runAnimation (anim >>= k) newObj remainingDelta
      (Right a, Just remainingDelta) -> -- run animation in k
        runAnimation (k a) newObj remainingDelta
\end{code}

\subsubsection{Par Instance}

In the \hs{Par} instance, we define how animations run in parallel. To do this, we apply both animation functions \hs{f1} and \hs{f2} after each other on the given state \hs{obj}. Deciding how to proceed the animation is done in a similar manner as the \hs{Monad} implementation, but is more laborious to implement since we need to take two remaining animations and two remaining time deltas into consideration.

\begin{code}
instance (Monad m) => Par (Animation obj m) where
  liftP2 combine (Animation f1) (Animation f2) =
    Animation $ \obj t -> do
    -- apply animation functions
    (obj1, remAnim1, mRem1) <- f1 obj t
    (obj2, remAnim2, mRem2) <- f2 obj1 t
    -- calculate remaining time delta
    let newRem = case (mRem1, mRem2) of
          (Nothing, _) -> Nothing
          (_, Nothing) -> Nothing
          (Just rem1, Just rem2) -> Just (min rem1 rem2)
    let newAnim = case (remAnim1, remAnim2) of
          (Right a, Right b) ->
            Right (combine a b)
          (Left aniA, Right b) ->
            Left (fmap (\a -> combine a b) aniA)
          (Right a, Left aniB) ->
            Left (fmap (\b -> combine a b) aniB)
          (Left aniA, Left aniB) ->
            Left (liftP2 combine aniA aniB)
    case (newRem, newAnim) of
      (Just rem, Left anim) -> runAnimation anim obj2 rem
      (_, _) -> return (obj2, newAnim, newRem)
\end{code}

\subsubsection{Examples}

TODO: examples of running animations by linking with gloss? or tuple world example with visual explanation?

\subsection{Inspecting Animations}

Inspecting an animation is done by interpreting \dsl{} to a different data type, which provides instances for the typeclasses with different semantics. This section implements support for calculating the duration of an animation.

\subsubsection{Inspecting Basic}

The data type we utilize for inspection is the \hs{Const} functor, defined below.

\begin{spec}
newtype Const a b = Const { getConst :: a }
\end{spec}

To inspect animations we provide an instance for the operations which interprets them into this data type. In the case of the \hs{basic} operation, we embed the duration within a \hs{Const} wrapper.

\begin{code}
instance Basic obj (Const Duration) where
  basic _ duration _ = Const duration
\end{code}

\subsubsection{Inspecting Applicative}

While the \hs{Const} data type does not provide a \hs{Monad} instance, it does provide an \hs{Applicative} instance. This means that it is not possible to inspect animations with a \hs{Monad} constraint, but it is possible for animations with an \hs{Applicative} constraint.

\subsubsection{Inspecting Par}

The duration of two animations in parallel is equal to the highest duration of the two. The \hs{Par} instance implementation for \hs{Const Duration} reflects this idea. 

\begin{code}
instance Par (Const Duration) where
  liftP2 _ (Const x1) (Const x2) = Const (max x1 x2)
\end{code}

\subsubsection{Examples}

The duration function is a specialization of the unwrapper function of the \hs{Const} data type, namely \hs{getConst}. We can feed our previously defined animations \hs{selectBtn2Anim} and \hs{menuIntro} from Section~\ref{sec:motivation} to this function and obtain the duration of the animations as a result.

\begin{code}
duration :: Const Duration a -> Duration
duration = getConst

selectBtn2AnimDuration :: Duration
selectBtn2AnimDuration = duration selectBtn2Anim
-- selectBtn2AnimDuration = For 1.0

menuIntroDuration :: Duration
menuIntroDuration = duration menuIntro
-- menuIntroDuration = For 0.5
\end{code}

When we try to retrieve the duration of an animation with a monad constraint, we receive an error from the compiler: it cannot find a \hs{Monad} instance for \hs{Const Duration}.

\begin{spec}
complicatedAnimationDuration :: Duration
complicatedAnimationDuration = duration complicatedAnimation
-- No instance for (Monad (Const Duration))
\end{spec}

\subsection{Adding a Custom Operation}

Adding a custom operation is done as in any other mtl-style approach: by defining a new class containing this operation. For example, if we want to add a \hs{set} operation, then we create a corresponding \hs{Set} class.

\begin{code}
class Set obj f where
  set :: Lens' obj a -> a -> f ()
\end{code}

Now, an animation containing the \hs{set} operation will additionally have a \hs{Set} constraint added to it.

\begin{code}
checkIcon :: (Set CompleteIcon f, ...) => f ()
checkIcon = do
  ...
  set (checkmark . color) completeGreen
  ...
\end{code}

To inspect or run such an animation, we also need to provide instances for the \hs{Animation} and \hs{Const} data types. In the \hs{Animation} instance, we alter the previous state by setting the value targeted by the \hs{lens} to \hs{a}. The duration of a \hs{set} animation is 0, which is what is returned in the \hs{Duration} instance.

\begin{code}
instance (Applicative m) => Set obj (Animation obj m) where
  set lens a = Animation $ \obj t -> let
    newObj = Lens.set lens a obj
    in pure (newObj, Right (), Just t)

instance Set obj (Const Duration) where
  set _ _ = Const (For 0)
\end{code}

This section gave an overview of the features provided by \dsl{}, however combining inspectability while allowing freedom of expressivity is not always straightforward. Therefore, we look at an example of the interaction of these two features in the next section.
