\section{Implementation of \dsl{}}
\label{sec:detail}

This section implements the previously introduced operations and redefines the animations to show the resulting type signature. We
develop \dsl{} in the style of the \texttt{mtl}
library\footnote{\url{http://hackage.haskell.org/package/mtl}} which implements
monadic effects using type classes \cite{DBLP:conf/afp/Jones95}.  This style is
also called the finally tagless approach \cite{DBLP:journals/jfp/CaretteKS09}.
However, because the \dsl{} classes are not subclasses of \hs{Monad}, they leave
room for inspectability.

\subsection{Specifying Basic Animations}

The \texttt{mtl} library uses type classes to declare the basic operations of an
effect. Similarly, we specify the \hs{linearTo} operation using
the \hs{LinearTo} type class.

\begin{code}
class LinearTo obj f where
  linearTo :: Traversal' s Float -> Duration -> Target -> f ()
\end{code}

The traditional mtl style would add a \hs{Monad f} superclass constraint. As 
it hinders inspectability, we defer the addition of this constraint to the user.
This allows the definition of animations which are, for example \hs{Applicative},
if inspectability is needed or \hs{Monad} if it is not.

The \hs{linearTo} function is used to specify basic
animations like \hs{line1Out}, \hs{line2In},
\hs{menuSlideIn}, and \hs{appFadeOut} from
Section~\ref{sec:motivation}. 
As an example, we redefine \hs{line1Out} with its type signature; the others 
are similar. 

\begin{code}
line1Out :: (LinearTo Application f) => f ()
line1Out = linearTo (navbar . underline1 . width) (For 0.25) (To 0)
\end{code}

\subsection{Specifying Composed Animations}

Section~\ref{sec:motivation} used the combinators \hs{sequential} and
\hs{parallel} for composing animations. In this section, we describe these combinators in more detail.

\subsubsection{Sequential Composition}

We reuse the \hs{Functor}-\hs{Applicative}-\hs{Monad} hierarchy for
sequencing animations.

ehe \hs{liftA2} function from the \hs{Applicative} class, which has type \\\hs{Applicative f =>}~\hs{(}\hs{a}~\hs{->}~\hs{b}~\hs{->}~\hs{c}\hs{)}~\hs{->}~\hs{f}~\hs{a}~\hs{->}~\hs{f}~\hs{b}~\hs{->}~\hs{f}~\hs{c}}, takes two animations \hs{f}~\hs{a} and \hs{f}~\hs{b} and returns a new animation which plays them in order. The final result of the animation is of type \hs{c}, which is obtained by using the function \hs{a}~\hs{->}~\hs{b}~\hs{->}~\hs{c}} and applying the results of the two played animations to it.

The \hs{>>=} function from the \hs{Monad} class, which has type \hs{Monad f}~\hs{=>}~\hs{f}~\hs{a}~\hs{->}\\\hs{(}\hs{a}~\hs{->}~\hs{f}~\hs{b}\hs{)}, takes an animation \hs{f a} and then feeds the result of this animation into the function \hs{a}~\hs{->}~\hs{f}~\hs{b} to play the animation \hs{f}~\hs{b}.


The \hs{sequential} function is a specialization of the \hs{liftA2} function. It only applies to animations with a \hs{()} return value, and trivially combines the results.

\begin{code}
sequential :: (Applicative f) => f () -> f () -> f ()
sequential f1 f2 = liftA2 (\_ _ -> ()) f1 f2
\end{code}

Hence, the type signature for \hs{selectBtn2Anim} contains an 
\hs{(Applicative f)} constraint in addition to the \hs{(LinearTo Application f)}
constraint.

\begin{code}
selectBtn2Anim :: (LinearTo Application f, Applicative f) => f ()
selectBtn2Anim = line1Out `sequential` line2In
\end{code}

\subsubsection{Parallel Composition}

We create our own \hs{Parallel} type class for the \hs{parallel} function\footnote{The \hs{Alternative} class (\url{https://en.wikibooks.org/wiki/Haskell/Alternative_and_MonadPlus}) is not suitable as the laws are not the same.}. Its \hs{liftP2} function has the same signature as \hs{liftA2}, but the intended semantics of the \hs{liftA2} implementation is parallel rather than sequential composition. Technically they are interchangeable, but the relation of \hs{Applicative} to \hs{Monad} makes it more sensible for sequential composition semantics. The \hs{parallel} function is a specialization of \hs{liftP2}.

\begin{code}
class Parallel f where
  liftP2 :: (a -> b -> c) -> f a -> f b -> f c

parallel :: (Parallel f) => f () -> f () -> f ()
parallel f1 f2 = liftP2 (\_ _ -> ()) f1 f2
\end{code}
With that in place we can give a type signature for \hs{menuIntro}.
\begin{code}
menuIntro :: (LinearTo Application f, Parallel f) => f ()
menuIntro = menuSlideIn `parallel` appFadeOut
\end{code}

\subsection{Running Animations}

Now we create a new \hs{Animation} data type that instantiates the above
type classes to interpret \dsl{} programs as actual animations. We briefly
summarize this implementation here and refer for more details to
our codebase.\footnote{\url{https://github.com/rubenpieters/anim_eff_dsl/tree/master/code}}

The \hs{Animation} data type, defined below, models an animation.
It takes the current state \hs{s} and the
time elapsed since the previous frame. It produces a new
state for the next frame, the remaining unused time and either the remainder of the animation or, if there is no remainder, the result of the animation. Note that the output is wrapped in a type
constructor \hs{m} to embed custom effects. 
We need the unused time when there is more time between frames than the animation
uses.
Then, the remaining time
can be used to run the rest of the animation.

\begin{code}
newtype Animation s m a = Animation { runAnimation ::
    s ->                             -- previous state
    Float ->                         -- time delta
    m ( s                            -- next state
      , Either (Animation s m a) a   -- remainder / result
      , Maybe Float )}               -- remaining delta time
\end{code}

\subsubsection{LinearTo Instance}

The \hs{linearTo} implementation of \hs{Animation} constructs
the new state, calculates the remainder of the animation and the remaining delta time. The difference between the \hs{linearTo} duration and the
frame time determines whether there is a remaining \hs{linearTo} animation or
remaining time.

\paragraph{Examples}

We illustrate the behaviour on a tuple state \hs{(Float, Float)}, of an \hs{x} and \hs{y} value.
The \hs{right} animation transforms the \hs{x} value to 50 over 1 second.

\begin{code}
right :: (LinearTo (Float, Float) f) => f ()
right = linearTo x (For 1) (To 50)
\end{code}

We run it for 0.5 seconds by applying it to the \hs{runAnimation} function,
together with the initial state (\hs{s0 =
(0,0)}) and the duration \hs{0.5}. We instantiate the \hs{m} type constructor
inside \hs{Animation} with \hs{Identity} as no additional effects are
needed; this means that the result can be unwrapped with
\hs{runIdentity}.

\begin{code}
(s1, remAn1, remDel1) = runIdentity (runAnimation right s0 0.5)
-- s1 = (25.0, 0.0) | remAn1 = Left anim2 | remDel1 = Nothing
\end{code}

Running \hs{right} for 0.5 seconds uses all available time and yields the new state
\hs{(25, 0)}. The remainder of
the animation is the \hs{right} animation with its duration reduced by \hs{0.5}, or essentially \hs{linearTo x (For 0.5) (To 50)}. Let us run this
remainder for 1 second.

\begin{code}
(s2, remAn2, remDel2) = runIdentity (runAnimation anim2 s1 1)
-- s2 = (50.0, 0.0) | remAn2 = Right () | remDel2 = Just 0.5
\end{code}

Now the final state is \hs{(50, 0)} with result \hs{()} and remaining time \hs{0.5}.

\subsubsection{Monad Instance}

For sequential animations we provide a \hs{Monad} instance. Its \hs{return} embeds the
result \hs{a} inside the \hs{Animation} data type. The essence of the
\hs{f}~\hs{>>=}~\hs{k} case is straightforward: first, run the animation
\hs{f}, then pass its result to the continuation \hs{k} and run that animation.
We return the result of the animation, or, if there is an animation remainder,
because the remaining time was used up, we return that remainder.

\paragraph{Examples}

Let us define an additional animation \hs{up} which transforms the \hs{y} value to 50 over a duration of 1 second. Additionally, we define an animation \hs{rightThenUp} which composes the \hs{right} and \hs{up} animations in sequence.

\begin{code}
up :: (LinearTo (Float, Float) f) => f ()
up = linearTo y (For 1) (To 50)

rightThenUp :: (LinearTo (Float, Float) f, Applicative f) => f ()
rightThenUp = right `sequential` up
\end{code}

Running the \hs{rightThenUp} animation for 0.5 seconds gives a similar result
to running \hs{right} for 0.5 seconds. We obtain the new state \hs{(25,
0)}, an animation remainder \hs{anim2} and there is no remaining time.
Now the animation remainder is the rest of \hs{rightThenUp},
which is half of \hs{right} and \hs{up}. So,
when we run this animation remainder for 1 second, it will run the second half
of \hs{right} and the first half of \hs{up}.  This results in the state
\hs{(50, 25)}, the animation remainder \hs{anim3} and no remaining delta time.
This animation remainder is of course the second half of the \hs{up} animation.
If we continue to run that remainder, for example for 1 second, then we get the
final state \hs{(50, 50)} and the animation result \hs{()}.

\subsubsection{Parallel Instance}

The \hs{liftP2} implementation runs
the animations \hs{f1} and \hs{f2} on the starting
state. We match on the cases where \hs{f1} and
\hs{f2} finish with a result or an animation remainder and remaining
time. We check which of the animations have finished and repackage them
either into a result or a new remainder, using the result combination function
where appropriate. When the longest of the two parallel animations is finished
while not fully using the remaining delta time, we continue running the remainder
of the animation.

\paragraph{Examples}

Let us run the animations \hs{right} and \hs{up} in parallel, which means
that both the \hs{x} and \hs{y} value will increase simultaneously.

\begin{code}
rightAndUp :: (LinearTo (Float, Float) f, Parallel f) => f ()
rightAndUp = right `parallel` up
\end{code}

The result of running this animation for 0.5 seconds gives the state \hs{(25,
25)} and no remaining time. If we continue the animation
remainder we get the state \hs{(50, 50)} and 0.5 seconds of remaining 
time.

\subsection{Inspecting Animations}

To inspect animations we instantiate them with \hs{Const}. It wraps an \hs{a} value and has a \hs{b} phantom type parameter to trivially make it a functor.

\begin{spec}
newtype Const a b = Const { getConst :: a }
\end{spec}

We might wonder why this extra work is necessary. After all, it is possible to obtain the duration of an animation by running the animation and keeping track of how long it takes. First, this is not an ideal approach for obtaining the duration. We might obtain erroneous results when doing this on conditional animations. Since only one branch of the conditional will be taken, while the other branch with a different duration might be taken in reality. Also, this approach is infeasible when there are effects embedded within the animation. Second, duration is one possible inspection target. Another example is tracking the used textures within an animation so they can be loaded automatically. For this to be possible we must run the inspection \emph{before} the animation runs for the first time, since the textures must be loaded first.

\subsubsection{Inspecting LinearTo}

To obtain the duration of a \hs{linearTo} animation, we embed the duration in the \hs{Const} wrapper.

\begin{code}
instance LinearTo obj (Const Duration) where
  linearTo _ duration _ = Const duration
\end{code}

\subsubsection{Inspecting Applicative}

It is not possible to
inspect animations with a \hs{Monad} constraint, but it is possible for
animations with an \hs{Applicative} constraint. The \hs{Const} data type is not
the culprit here, but rather the \hs{>>=} method of the \hs{Monad} class, which
contains the limiting factor: a continuation function \hs{a}~\hs{->}~\hs{m}~\hs{b}.

\subsubsection{Inspecting Parallel}

The duration of two parallel animations is the maximum of their durations. 
The \hs{Par (Const Duration)} instance implements this. 

\begin{code}
instance Par (Const Duration) where
  liftP2 _ (Const x1) (Const x2) = Const (max x1 x2)
\end{code}

\paragraph{Examples}

The duration function is a specialization of the unwrapper function of the
\hs{Const} data type, namely \hs{getConst}. We can feed our previously defined
animations \hs{selectBtn2Anim} and \hs{menuIntro} from
Section~\ref{sec:motivation} to this function and obtain their durations as a result.

\begin{code}
duration :: Const Duration a -> Duration
duration = getConst

selectBtn2AnimDuration :: Duration
selectBtn2AnimDuration = duration selectBtn2Anim -- = For 1.0

menuIntroDuration :: Duration
menuIntroDuration = duration menuIntro -- = For 0.5
\end{code}

When we try to retrieve the duration of a monadic animation,
there is an error from the compiler: there is no \hs{Monad} instance for
\hs{Const Duration}.

\begin{spec}
complicatedAnimDuration :: Duration
complicatedAnimDuration = duration complicatedAnim
-- No instance for (Monad (Const Duration))
\end{spec}

\subsection{Adding a Custom Operation}

Custom operation are added by defining a corresponding class. For example, if we want to add a \hs{set} operation, then we create the corresponding \hs{Set} class.

\begin{code}
class Set obj f where set :: Lens' obj a -> a -> f ()
\end{code}

Now, an animation using the \hs{set} operation will incur a \hs{Set} constraint.

\begin{code}
checkIcon :: (Set CompleteIcon f, ...) => f ()
checkIcon = do ...; set (checkmark . color) green; ...
\end{code}

To inspect or run such an animation, we also need to provide instances for the \hs{Animation} and \hs{Const} data types. In the \hs{Animation} instance, we alter the previous state by setting the value targeted by the \hs{lens} to \hs{a}. The duration of a \hs{set} animation is 0, which is what is returned in the \hs{Duration} instance.

\begin{code}
instance (Applicative m) => Set obj (Animation obj m) where
  set lens a = Animation $ \obj t -> let
    newObj = Lens.set lens a obj
    in pure (newObj, Right (), Just t)

instance Set obj (Const Duration) where
  set _ _ = Const (For 0)
\end{code}

